buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath(ProjectDeps.gradlePlugin)
        classpath(ProjectDeps.kotlinPlugin)
        classpath(ProjectDeps.kotlinSerialization)
        classpath(ProjectDeps.kotlinExtensions)
        classpath(ProjectDeps.firebasePlugin) {
            exclude group: "com.google.guava", module: "guava-jdk5"
        }
        classpath(ProjectDeps.googleServices)
        classpath(ProjectDeps.crashlytics)
        classpath(ProjectDeps.dokka)
    }
}

plugins {
    id "org.sonarqube" version "3.0"
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven {
            url "https://maven.google.com/"
        }
    }
    afterEvaluate {
        tasks.withType(JavaCompile.class) {
            options.compilerArgs << "-Xmaxerrs" << "500"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {

    if (it.name == "app") {
        apply plugin: "com.android.application"
    } else {
        apply plugin: "com.android.library"
    }

    apply plugin: "kotlin-android"
    apply plugin: "kotlin-android-extensions"
    apply plugin: "kotlin-kapt"
    apply plugin: "kotlinx-serialization"

    android {
        compileSdkVersion Build.compileSdkVersion
        defaultConfig {
            buildToolsVersion Build.buildToolsVersion
            minSdkVersion Build.minSdkVersion
            targetSdkVersion Build.targetSdkVersion
            compileSdkVersion Build.compileSdkVersion
            versionName "3.0.0"
            vectorDrawables.useSupportLibrary = true
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }
        lintOptions {
            abortOnError false
        }
        dexOptions {
            preDexLibraries = true
        }
        testOptions {
            animationsDisabled = true
            unitTests {
                returnDefaultValues = true
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        packagingOptions {
            exclude "META-INF/proguard/androidx-annotations.pro"
        }
    }

    dependencies {
        implementation(Libs.kotlin)
    }
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.projectName", "GdzieTaBiedra"
        property "sonar.projectKey", "asvid_GdzieTaBiedra"
        property "sonar.language", "kotlin"
        property "sonar.sources", "app/src/main/java"
        property "sonar.java.sources", "app/src/main/java"
        property "sonar.java.binaries", "app/build/intermediates/classes/."
    }
}
